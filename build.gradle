/*
 *     Orbit, a versatile image analysis software for biological image-based quantification.
 *     Copyright (C) 2009 - 2016 Actelion Pharmaceuticals Ltd., Gewerbestrasse 16, CH-4123 Allschwil, Switzerland.
 *
 *     This program is free software: you can redistribute it and/or modify
 *     it under the terms of the GNU General Public License as published by
 *     the Free Software Foundation, either version 3 of the License, or
 *     (at your option) any later version.
 *
 *     This program is distributed in the hope that it will be useful,
 *     but WITHOUT ANY WARRANTY; without even the implied warranty of
 *     MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *     GNU General Public License for more details.
 *
 *     You should have received a copy of the GNU General Public License
 *     along with this program.  If not, see <http://www.gnu.org/licenses/>.
 *
 */

buildscript {
    repositories {
        mavenCentral()
        //Needed only for SNAPSHOT versions
        //maven { url "http://oss.sonatype.org/content/repositories/snapshots/" }
    }
    dependencies {
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3"
    }
}

apply plugin: 'java'
apply plugin: 'maven-publish'


group = 'com.actelion.research'
ext.versionStr = '1.1.2'
ext.version=versionStr

description = 'mapReduceExecSpark'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'com.actelion.research'
            artifactId 'map-reduce-exec-spark'
            version versionStr
            from components.java
            artifact sourceJar {
                classifier "sources"
            }
            artifact docJar {
                classifier "javadoc"
            }
            pom.withXml {
                asNode().appendNode('description', 'Map Reduce Executor Spark (map-reduce-generic executor implementation)')
            }
        }
    }
}

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

task sourceJar(type: org.gradle.api.tasks.bundling.Jar) {
    from sourceSets.main.allJava
}
task docJar(type: org.gradle.api.tasks.bundling.Jar, dependsOn: 'javadoc') {
    from javadoc.destinationDir
}


task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'Spark Executor',
                'Implementation-Version': versionStr,
                'Main-Class': 'com.actelion.research.mapReduceExecSpark.executors.DeploySpark',
                'Vendor': 'Idorsia'
    }
    baseName = project.name + '-all'
    //zip64 = true
    from { configurations.compile.filter {it.name.toLowerCase().endsWith(".jar")}.collect { it.isDirectory() ? it : zipTree(it) } }
            {
                exclude 'META-INF/MANIFEST.MF'
                exclude "META-INF/*.SF"
                exclude "META-INF/*.DSA"
                exclude "META-INF/*.RSA"
                exclude "META-INF/LICENSE.txt"
                exclude "META-INF/NOTICE.txt"
            }
    with jar
}



dependencies {
    compile 'com.actelion.research:map-reduce-generic:1.0.10'
    //compile 'org.apache.hadoop:hadoop-client:1.6.1'      // compileOnly
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.5'
    compile group: 'org.apache.commons', name: 'commons-io', version: '1.3.2'
    compile (group: 'org.codelibs', name:'jcifs', version:'1.3.18.3') {  // 1.3.18.1 for SmbUtils     (exclude javax.servlet-api because Spark needs a different version)
        exclude (group:'javax.servlet', module: 'servlet-api')
    }

    compileOnly(group: 'org.apache.spark', name: 'spark-core_2.11', version: '2.2.1') {
        exclude(module: 'servlet-api')
    }
    compileOnly group: 'org.apache.spark', name: 'spark-sql_2.11', version: '2.2.1'
    testCompile 'junit:junit:[4.0,)'
}

if (file('custom.gradle').exists()) {
    apply from: 'custom.gradle'
}